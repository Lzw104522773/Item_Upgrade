// shared soa-tob code
INCLUDE ~itemupgrade/lib/items_common.tpa~

COPY ~itemupgrade/itm/c2leat01.itm~ ~override~
  SAY NAME2     @142
  SAY DESC      @143
  PATCH_IF enhanced_edition BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = "-1" opcode = 318 target = 1 timing = 2 STR_VAR resource = wand13 END // ee protection from wand of cloudkill
  END

COPY_EXISTING ~arow15.itm~ ~override~ // get arrow +3 projectile
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      READ_SHORT (abil_off + 0x2a + (index * 0x38)) ar3_proj
      SET index = abil_num // kill loop
    END
  END 
  BUT_ONLY

COPY_EXISTING ~bolt09.itm~ ~override~ // get bolt +3 projectile
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      READ_SHORT (abil_off + 0x2a + (index * 0x38)) bolt3_proj
      SET index = abil_num // kill loop
    END
  END 
  BUT_ONLY

COPY_EXISTING ~bull05.itm~ ~override~ // get bullet +3 projectile
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      READ_SHORT (abil_off + 0x2a + (index * 0x38)) bull3_proj
      SET index = abil_num // kill loop
    END
  END 
  BUT_ONLY

COPY ~itemupgrade/itm/c2quiv01.itm~ ~override~
  SAY NAME2     @108
  SAY DESC      @109
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      WRITE_SHORT (abil_off + 0x2a + (index * 0x38)) ar3_proj
      SET index = abil_num // kill loop
    END
  END 
  PATCH_IF enhanced_edition BEGIN // make usable by shaman
    WRITE_BYTE 0x21 (THIS BAND `BIT6) // removes druid flag
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = 11 parameter2 = 5 timing = 2 special = 1080 END
  END  

COPY ~itemupgrade/itm/c2quiv02.itm~ ~override~
  SAY NAME2     @110
  SAY DESC      @111
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      WRITE_SHORT (abil_off + 0x2a + (index * 0x38)) bolt3_proj
      SET index = abil_num // kill loop
    END
  END 

COPY ~itemupgrade/itm/c2quiv03.itm~ ~override~
  SAY NAME2     @112
  SAY DESC      @113
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
    PATCH_IF type = 2 BEGIN
      WRITE_SHORT (abil_off + 0x2a + (index * 0x38)) bull3_proj
      SET index = abil_num // kill loop
    END
  END 

COPY ~itemupgrade/itm/c2sw2h02.itm~ ~override~
  SAY NAME2     @170
  SAY DESC      @171
  PATCH_IF enhanced_edition BEGIN
    PATCH_IF FILE_EXISTS_IN_GAME ~#curedef.spl~ BEGIN // ee fixpack
      LPF ALTER_EFFECT INT_VAR match_opcode = 81 opcode = 146 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~#curedef~ END   
      LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 122 END
      LPF cd_apply_batch STR_VAR array_name = cd_cure_deafness END
    END
    PATCH_IF FILE_EXISTS_IN_GAME ~#curefbm.spl~ BEGIN // ee fixpack
      LPF ALTER_EFFECT INT_VAR match_opcode = 77 opcode = 146 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~#curefbm~ END   
      LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 48 END
    END  
  END ELSE BEGIN 
    PATCH_IF FILE_EXISTS_IN_GAME ~cditmdm0.spl~ THEN BEGIN // if Fixpack's item dispel magic spell is installed
      LPF ALTER_EFFECT INT_VAR match_opcode = 58 opcode = 146 parameter1 = 0 parameter2 = 1 STR_VAR resource = ~cditmdm0~ END // change dispel to cast spell
      // delete associated dispel magic effects
      LPF DELETE_EFFECT INT_VAR match_opcode =  77 END // cure feeblemind
      LPF DELETE_EFFECT INT_VAR match_opcode =  81 END // cure deafness
      LPF DELETE_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14056 END // 'dispel effects' string
      LPF DELETE_EFFECT INT_VAR match_opcode = 177 match_parameter1 = 7 match_parameter2 = 7 STR_VAR match_resource = destself END // destroy illusionary creatures
      LPF DELETE_EFFECT INT_VAR match_opcode = 215 STR_VAR match_resource = spdispma END // DM visuals
      LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 =  48 END // remove icon: feeblemind
      LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 112 END // remove icon: deaf
    END
  END  

COPY ~itemupgrade/bam/c2leat01.bam~ ~override~
     ~itemupgrade/bam/c2quiv01.bam~ ~override~
     ~itemupgrade/bam/c2quiv02.bam~ ~override~
     ~itemupgrade/bam/c2quiv03.bam~ ~override~
     ~itemupgrade/bam/c2sw2h02.bam~ ~override~
    
COPY_EXISTING ~itemexcl.2da~ ~override~ // add to magic ring/magic armor exclusion list
  REPLACE_TEXTUALLY CASE_INSENSITIVE ~^LEAT21\([ %TAB%].+\)$~ ~LEAT21\1
c2leat01\1~
  BUT_ONLY IF_EXISTS

// prompts random StringHeads if Lil +5 is being used
EXTEND_BOTTOM ~baldur25.bcs~ ~itemupgrade/bcs/lilarcor.baf~

// Restored Quiver of Plenty icons
COPY_EXISTING ~quiver01.itm~ ~override~
              ~quiver03.itm~ ~override~
  WRITE_ASCII 0x3a ~iquiver0~
  WRITE_ASCII 0x44 ~gquiver0~
  BUT_ONLY

COPY_EXISTING ~quiver02.itm~ ~override~
              ~quiver04.itm~ ~override~
  WRITE_ASCII 0x44 ~gquiver0~
  BUT_ONLY

ACTION_IF enhanced_edition = 2 BEGIN

  INCLUDE ~itemupgrade/lib/ee_game.tpa~ // function to redo descriptions

END

ACTION_IF MOD_IS_INSTALLED ~stratagems/setup-stratagems.tp2~ ~4250~ THEN BEGIN  // if SCS removes contingency scrolls

  EXTEND_BOTTOM ~ar4500.bcs~ ~itemupgrade/bcs/scs_compat.baf~ // adds an area var for crom and cespy to check in-game (crom has no script)

END  
